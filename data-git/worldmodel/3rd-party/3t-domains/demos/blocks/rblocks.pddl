(in-package :AP)

(define (domain rblocks)
    (:documentation "Based on Elaine Rich, Artificial Intelligence, 1983")
  (:requirements :true-negation)
  (:types block - object)
  (:predicates (on ?x ?y - block)
               (clear ?x - block)
               (ontable ?x - block)
               (holding ?x - block)
               (armempty))
  (:axiom 
    :vars (?b - block)
    :context (forall (?b2 - block)	; this does not work 12/16/2016
	       (not (on ?b2 ?b)))
    :implies (clear ?b)
    :documentation "nothing on ?b1 => clear")
  (:action stack
           :parameters (?x ?y - block)
           :precondition (and (clear ?y)
                              (holding ?x)
			      (not (= ?x ?y)))
           :effect (and (armempty)
                        (not (holding ?x))
                        (on ?x ?y)
                        (not (clear ?y))))
  (:action unstack
	   :parameters (?x ?y - block)
	   :precondition (and (on ?x ?y)
			      (clear ?x)
			      (armempty)
			      (not (= ?x ?y)))
	   :effect (and (not (on ?x ?y))
			(holding ?x)
			(not (armempty))
			(not (clear ?x))
			(clear ?y))
	   :documentation "?x is on another block")
  (:action pickup
	   :parameters (?x - block)
	   :precondition (and (ontable ?x)
			      (clear ?x)
			      (armempty))
	   :effect (and (holding ?x)
			(not (armempty))
			(not (clear ?x))
			(not (ontable ?x)))
	   :documentation "?x is on the table")
  (:action putdown
           :parameters (?x - block)
           :precondition (holding ?x)
           :effect (and (ontable ?x)
                        (armempty)
                        (not (holding ?x)))))

;;; problems (current problem bound to *problem*)

(define (situation four-blocks)
    (:domain rblocks)
  (:objects a b c d - block)
  (:init (on b a)
         (ontable a)
	 (armempty)))

(define (problem 2-stacks)
    (:domain rblocks)
  (:situation four-blocks)
  (:init (ontable c)
         (ontable d))
  (:goal (and (on c a)
              (on b d)
              (ontable a)
              (ontable d))))

(define (problem trivial)
    (:domain rblocks)
  (:situation four-blocks)
  (:init (clear b))			; axiom did not work
  (:goal (ontable b)))
